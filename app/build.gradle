// Manifest version information!
def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0

apply plugin: 'com.android.application'


apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../analysis/analysis.gradle'

def preDexEnabled = "true" == System.getProperty("pre-dex", "true")
// ToDo Update project name
def projectName = "praxis-mvvm-kotlin"

android {
  compileSdkVersion COMPILE_SDK_VERSION
  buildToolsVersion BUILD_TOOLS_VERSION

  defaultConfig {
    applicationId "com.mutualmobile.praxis"

    minSdkVersion MIN_SDK_VERSION
    targetSdkVersion TARGET_SDK_VERSION

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    vectorDrawables.useSupportLibrary = true
  }

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'LICENSE.txt'
  }

  dexOptions {
    preDexLibraries = preDexEnabled
  }

  dataBinding {
    enabled = true
  }

  buildTypes {
    release {
      debuggable false
      versionNameSuffix '-release'

      minifyEnabled true
      shrinkResources true

      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-common.txt', 'proguard-specific.txt'

      buildConfigField 'boolean', "ENABLE_LOGGING", 'false'
    }
    debug {
      debuggable true

      versionNameSuffix '-debug'
      applicationIdSuffix '.debug'

      buildConfigField 'boolean', "ENABLE_LOGGING", 'true'
    }
  }

  lintOptions {
    lintConfig file('../analysis/lint/lint.xml')
    htmlOutput file("$project.buildDir/reports/lint/lint-reports.html")
    xmlOutput file("$project.buildDir/reports/lint/lint-reports.xml")

    abortOnError false
  }
}

// Required for annotation processing plugins like Dagger
kapt {
  generateStubs = true
}

dependencies {

  // Kotlin
  implementation deps.kotlinsdk

  // Android Support Libs
  implementation deps.appcompatv7
  implementation deps.design

  // Data Binding
  implementation 'com.android.support:support-v4:27.1.0'
  implementation 'com.android.support.constraint:constraint-layout:1.0.2'
  kapt deps.databinding

  // Dagger 2
  implementation deps.dagger
  implementation deps.daggerSupport
  kapt deps.daggerCompiler
  kapt deps.daggerProcessor

  // Retrofit 2
  implementation deps.retrofit
  implementation deps.retrofitmoshi
  implementation deps.retrofitrx
  implementation deps.okhttplogger

  // Rx
  implementation deps.rxjava2
  implementation deps.rxandroid2

  // Rx Plugins
  implementation deps.rxpermissions

  // Timber
  implementation deps.timber

  implementation deps.archExtenstion
  implementation deps.archViewmmodel
  implementation deps.archLivdata
  implementation deps.reactiveStreams
  kapt deps.archCompiler

  // UI Testing
  androidTestImplementation(deps.espressocore, {
    exclude group: 'com.android.support', module: 'support-annotations'
  })

  // Unit Testing
  testImplementation deps.junit
}

